+ [[../index.org][Main Index]]
+ [[./index.org][Problems]]

* Lexicographic permutations
*** Problem 24
A permutation is an ordered arrangement of objects. For example, 3124 is one
possible permutation of the digits 1, 2, 3 and 4. If all of the permutations are
listed numerically or alphabetically, we call it lexicographic order. The
lexicographic permutations of 0, 1 and 2 are:

012   021   102   120   201   210

What is the millionth lexicographic permutation of the digits 0, 1, 2, 3, 4, 5,
6, 7, 8 and 9?

*** Clojure
#+BEGIN_SRC clojure
  (defn n! [n] (reduce * 1N (range 1 (inc n))))

  (defn n-*-permutations [n remaining-options]
    (* n (n! (dec (count remaining-options)))))

  (defn under-target? [remaining-target current-index remaining-options]
    (> remaining-target
       (n-*-permutations (inc current-index) remaining-options)))

  (defn remove-element-from [n from]
    (vec (remove #{n} from)))

  (defn target-difference [target n options]
    (- target (n-*-permutations n options)))

  (defn nth-lexicographic-permutation-helper [remaining-target current-index remaining-options built-up-number]
    (cond
      (not (seq remaining-options)) built-up-number
      (under-target? remaining-target
                     current-index
                     remaining-options) (recur remaining-target
                                               (inc current-index)
                                               remaining-options
                                               built-up-number)
      :else (let [next-element (nth remaining-options current-index)]
              (recur (target-difference remaining-target current-index remaining-options)
                     0
                     (remove-element-from next-element remaining-options)
                     (conj built-up-number next-element)))))

  (defn nth-lexicographic-permutation [n my-seq]
    (nth-lexicographic-permutation-helper n 0 my-seq []))

  (->> (nth-lexicographic-permutation 1000000 [0 1 2 3 4 5 6 7 8 9])
       (map str)
       (apply str)
       (BigDecimal.)) ;; => 2783915460


#+END_SRC
