+ [[../index.org][Main Index]]
+ [[./index.org][Problems]]

* Number letter counts
*** Problem 17
If the numbers 1 to 5 are written out in words: one, two, three, four, five,
then there are 3 + 3 + 5 + 4 + 4 = 19 letters used in total.

If all the numbers from 1 to 1000 (one thousand) inclusive were written out in
words, how many letters would be used?

NOTE: Do not count spaces or hyphens. For example, 342 (three hundred and
forty-two) contains 23 letters and 115 (one hundred and fifteen) contains 20
letters. The use of "and" when writing out numbers is in compliance with British
usage.

*** Clojure
#+BEGIN_SRC clojure
  (require '[clojure.pprint :as pprint])

  (def num->word (partial pprint/cl-format nil "~R"))

  (defn num->british-word
    [n]
    (if (and (> n 100)
             (not (zero? (mod n 100))))
      (let [as-word (num->word n)
            last-space (.lastIndexOf as-word " " )
            before-last-space (subs as-word 0 last-space)
            after-last-space (subs as-word (inc last-space))]
        (str before-last-space " and " after-last-space))
      (num->word n)))

  (defn char-range
    [from to]
    (map char (range (int from)
                     (inc (int to)))))

  (defn clean-up-word
    [word]
    (->> word
         clojure.string/lower-case
         (keep (set (char-range \a \z)))
         (apply str)))

  (defn count-letters
    [word]
    (-> word
        clean-up-word
        count))

  (->> (range 1 (inc 1000))
       (map num->british-word)
       (map count-letters)
       (reduce +)) ;; => 21124
#+END_SRC
